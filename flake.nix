{
  description = "Lefthook-compatible shellcheck check";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";
    nix-lefthook-git-conflict-markers = {
      url = "github:pr0d1r2/nix-lefthook-git-conflict-markers";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nix-lefthook-git-no-local-paths = {
      url = "github:pr0d1r2/nix-lefthook-git-no-local-paths";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nix-lefthook-missing-final-newline = {
      url = "github:pr0d1r2/nix-lefthook-missing-final-newline";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nix-lefthook-trailing-whitespace = {
      url = "github:pr0d1r2/nix-lefthook-trailing-whitespace";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      nix-lefthook-git-conflict-markers,
      nix-lefthook-git-no-local-paths,
      nix-lefthook-missing-final-newline,
      nix-lefthook-trailing-whitespace,
    }:
    let
      supportedSystems = [
        "aarch64-darwin"
        "x86_64-darwin"
        "x86_64-linux"
        "aarch64-linux"
      ];
      forAllSystems =
        f: nixpkgs.lib.genAttrs supportedSystems (system: f nixpkgs.legacyPackages.${system});
    in
    {
      packages = forAllSystems (pkgs: {
        default = pkgs.writeShellApplication {
          name = "lefthook-shellcheck";
          runtimeInputs = [ pkgs.shellcheck ];
          text = builtins.readFile ./lefthook-shellcheck.sh;
        };
      });

      devShells = forAllSystems (
        pkgs:
        let
          batsWithLibs = pkgs.bats.withLibraries (p: [
            p.bats-support
            p.bats-assert
            p.bats-file
          ]);
        in
        {
          default = pkgs.mkShell {
            packages = [
              self.packages.${pkgs.stdenv.hostPlatform.system}.default
              pkgs.shellcheck
              nix-lefthook-git-conflict-markers.packages.${pkgs.stdenv.hostPlatform.system}.default
              nix-lefthook-git-no-local-paths.packages.${pkgs.stdenv.hostPlatform.system}.default
              nix-lefthook-missing-final-newline.packages.${pkgs.stdenv.hostPlatform.system}.default
              nix-lefthook-trailing-whitespace.packages.${pkgs.stdenv.hostPlatform.system}.default
              batsWithLibs
              pkgs.editorconfig-checker
              pkgs.typos
              pkgs.yamllint
              pkgs.git
              pkgs.lefthook
              pkgs.nixfmt
              pkgs.shfmt
              pkgs.statix
              pkgs.deadnix
            ];
            shellHook = builtins.replaceStrings [ "@BATS_LIB_PATH@" ] [ "${batsWithLibs}" ] (
              builtins.readFile ./dev.sh
            );
          };
        }
      );
    };
}
